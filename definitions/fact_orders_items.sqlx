config {
    type: "table",
    description: "Final fact table joining items, orders, and products. Optimized with partitioning and clustering.",
    bigquery: {
        partitionBy: "DATE(purchase_at)",
        clusterBy: ["product_category_name", "order_status"]
    },
    assertions: {
        uniqueKey: ["order_id", "product_id"],
        nonNull: ["purchase_at", "total_order_item_value"]
    },
    columns: {
        total_order_item_value: "Total value of the item including price and freight"
    }
}

SELECT
  i.order_id,
  i.product_id,
  o.purchase_at,
  o.order_status,
  p.product_category_name,
  i.price,
  i.freight_value,
  (i.price + i.freight_value) as total_order_item_value
FROM
  ${ref("stg_items")} AS i
LEFT JOIN
  ${ref("stg_orders")} AS o ON i.order_id = o.order_id
LEFT JOIN
  ${ref("stg_products")} AS p ON i.product_id = p.product_id
